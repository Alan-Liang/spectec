;;
;; General definitions
;;

;; General variables

var i : nat
var j : nat
var k : nat

syntax N hint(macro none) = nat  ;; hack
syntax M hint(macro none) = nat  ;; hack
syntax K hint(macro none) = nat  ;; hack
syntax n hint(macro none) = nat  ;; hack
syntax m hint(macro none) = nat  ;; hack

var ii : int hint(show i)  ;; hack


;; General constants

def $Ki : nat hint(macro none)
def $Ki = 1024


;; General functions

def $min(nat, nat) : nat
def $min(0, j) = 0
def $min(i, 0) = 0
def $min($(i+1), $(j+1)) = $min(i, j)

def $sum(nat*) : nat  hint(show (+) %) hint(macro none)
def $sum(eps) = 0
def $sum(n n'*) = $(n + $sum(n'*))


;; General sequence functions

def $concat_(syntax X, (X*)*) : X*  hint(show (++) %2)
def $concat_(syntax X, eps) = eps
def $concat_(syntax X, (w*) (w'*)*) = w* $concat_(X, (w'*)*)

def $disjoint_(syntax X, X*) : bool  hint(show %2 $disjoint) hint(macro none)
def $disjoint_(syntax X, eps) = true
def $disjoint_(syntax X, w w'*) = ~(w <- w'*) /\ $disjoint_(X, w'*)

;; [1; 2]; [a; b; c] -> [1; a]; [1; b]; [1; c]; [2; a]; [2; b]; [2; c]
def $listproduct_(syntax X, (X*)*) : (X*)*
def $lpaux_(syntax X, X*, (X*)*) : (X*)*
def $lpaux2_(syntax X, X, (X*)*) : (X*)*

def $listproduct_(syntax X, eps)         = (eps)
def $listproduct_(syntax X, (w*) (w'*)*) = $lpaux_(X, w*, $listproduct_(X, w'**))

def $lpaux_(syntax X, eps,   (w''*)*) = eps
def $lpaux_(syntax X, w w'*, (w''*)*) = $lpaux2_(X, w, (w''*)*) $lpaux_(X, w'*, (w''*)*)

def $lpaux2_(syntax X, w, eps)           = eps
def $lpaux2_(syntax X, w, (w'*) (w''*)*) = (ww*) $lpaux2_(X, w, (w''*)*)
  -- if ww* = w w'*
