;;
;; Addresses
;;

syntax addr hint(desc "address") = nat
syntax funcaddr hint(desc "function address") = addr

var a : addr
var fa : funcaddr


;;
;; Values
;;

;; Basic values

syntax val hint(desc "value") =
  | CONST valtype num_(valtype)   hint(show %.CONST %)

var v : val


;; External values

syntax externval hint(desc "external value") =
  | FUNC funcaddr

var xv : externval


;;
;; Configurations
;;

;; Instances

syntax funcinst hint(desc "function instance") =
  { TYPE functype,
    MODULE moduleinst,
    CODE func }
syntax exportinst hint(desc "export instance") =
  { NAME name,
    VALUE externval }

syntax moduleinst hint(desc "module instance") =
  { TYPES functype*,
    FUNCS funcaddr*,
    EXPORTS exportinst* }

var mm : moduleinst
var fi : funcinst
var xi : exportinst


;; Configurations proper

syntax store hint(desc "store") =
  { FUNCS funcinst* }

syntax frame hint(desc "frame") =
  { LOCALS val*,
    MODULE moduleinst }

syntax state hint(desc "state") = store; frame
syntax config hint(desc "configuration") = state; admininstr*

var s : store
var f : frame
var z : state


;;
;; Administrative Instructions
;;

syntax admininstr hint(show instr) hint(desc "administrative instruction") =
  | instr
  | CALL_ADDR funcaddr             hint(show CALL %)
  | LABEL_ n `{instr*} admininstr* hint(show LABEL_%#% %%)
  | FRAME_ n `{frame} admininstr*  hint(show FRAME_%#% %%)
  | TRAP

def $funcaddr(state) : funcaddr*      hint(show %.MODULE.FUNCS)
def $funcaddr((s; f)) = f.MODULE.FUNCS


def $local(state, localidx) : val           hint(show %.LOCALS[%])
def $local((s; f), x) = f.LOCALS[x]

def $with_local(state, localidx, val) : state               hint(show %[.LOCALS[%] = %])
def $with_local((s; f), x, v) = s; f[.LOCALS[x] = v]

def $funcinst(state) : funcinst*      hint(show %.FUNCS)
def $funcinst((s; f)) = s.FUNCS

def $default_(valtype) : val

def $default_(I32) = (CONST I32 0)
def $default_(I64) = (CONST I64 0)